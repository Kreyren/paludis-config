#!/usr/bin/awk -f

# Created by Kumool based on original regular expression provided by Jacob Hrbek <kreyren@rixotstudio.cz> for Jacob Hrbek <kreyren@rixotstudio.cz> in 2020 under GPL-3 license <https://www.gnu.org/licenses/gpl-3.0.en.html>

# This is an AWK script that expects stout of 'cave resolve pkg' to create unmasks in /etc/paludis/platforms.conf
# Example usage: 'cave resolve world -c | palumasker'
# Example output: 'sys-apps/paludis:0::arbor ~amd64'

# awk -f ./kreyren.awk kreyren-input1 | wc -l (?)

!e && /^I encountered the following errors:$/ {
 e++ # set e to 1, e is error section
 next # don't do anything else
}

# if e is defined (which is error section) and line/ matches regex, set pkg to $2
# $2 is the second field in line/record
e && /^!/ {pkg=$2;next}

# match sets RSTART and RLENGTH to whatever it matches
e && pkg && /:[^:]+::installed$/ { installed = $2;next }
e && installed && match($0,/:[^:]+::[^:]+$/) {
 arbor= substr($0,RSTART,RLENGTH) # substr extracts a section from the current string
 installed=0
 next
}

e && arbor && /Supported platforms ~amd64/ {
 print pkg arbor " " $3
 pkg=arbor="" # set both pkg and arbor to "" which means empty
 next
}

# Second section of text
/I cannot proceed without being permitted to do the following:/ {e=0;e2++;next}

e2 && /^n   [^/]+\/[^:]+:[0-9]+::/ {pkg=$2}
e2 && pkg && /Supported platforms ~amd64/ {print pkg " ~amd64";m=""}
